file       : statement* EOF ;

statement  : assignment
           | print_stmt
           | if_stmt
           | while_stmt
           | category_stmt
           ;

assignment  : IDENTIFIER '=' (expression | cat_init) NEWLINE ;
print_stmt  : 'print' expression NEWLINE ;

block       : NEWLINE INDENT statement+ DEDENT ;

cat_init : IDENTIFIER '(' term* ')' ;
category_block : NEWLINE INDENT 'obj' ':' object_block 'hom' ':' homset DEDENT ;
object_block   :  NEWLINE INDENT objects DEDENT ;
homset      :  NEWLINE (INDENT morphism+ DEDENT)? ;

objects     : object (NEWLINE? object)* NEWLINE ;
morphism    : object '->' object+ NEWLINE ;
object      : term ;

if_stmt     : 'if' expression ':' block ( elif_stmt | else_block )? ;
elif_stmt   : 'elif' expression ':' block ( elif_stmt | else_block )? ;
else_block  : 'else' ':' block ;

while_stmt  : 'while' expression ':' block ;

category_stmt : 'cat' IDENTIFIER '(' IDENTIFIER* ')' ':' category_block ;

expression  : disjunction ;

disjunction : conjunction ( 'or' conjunction )* ;
conjunction : inversion ( 'and' inversion )* ;
inversion   : 'not' inversion | membership;

membership  : comparison ( ('->' comparison)? 'in' IDENTIFIER )? ;

comparison  : sum ( ( '==' | '!=' | '<' | '>' ) sum )* ;
sum         : term ( ( '+' | '-' ) term )* ;

term        : '-' term | atom ;
atom        : IDENTIFIER | NUMBER | '(' expression ')' ;
